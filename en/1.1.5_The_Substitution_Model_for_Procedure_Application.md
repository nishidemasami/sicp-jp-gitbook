### <span class="chapnum">1.1.5</span> The Substitution Model for Procedure Application

To evaluate a combination whose operator names a compound procedure, the
interpreter follows much the same process as for combinations whose operators
name primitive procedures, which we described in [Section 1.1.3](1.1.3_Evaluating_Combinations.md).  That is,
the interpreter evaluates the elements of the combination and applies the
procedure (which is the value of the operator of the combination) to the
arguments (which are the values of the operands of the combination).

We can assume that the mechanism for applying primitive procedures to arguments
is built into the interpreter.  For compound procedures, the application
process is as follows:

> To apply a compound procedure to arguments, evaluate the body of the procedure with each formal parameter replaced by the corresponding argument.


To illustrate this process, let's evaluate the combination

```scheme
(f 5)
```


where $\texttt{f}$ is the procedure defined in [Section 1.1.4](1.1.4_Compound_Procedures.md).  We begin by
retrieving the body of $\texttt{f}$:

```scheme
(sum-of-squares (+ a 1) (* a 2))
```


Then we replace the formal parameter $\texttt{a}$ by the argument 5:

```scheme
(sum-of-squares (+ 5 1) (* 5 2))
```


Thus the problem reduces to the evaluation of a combination with two operands
and an operator $\texttt{sum-of-squares}$.  Evaluating this combination involves
three subproblems.  We must evaluate the operator to get the procedure to be
applied, and we must evaluate the operands to get the arguments.  Now $\texttt{(+
5 1)}$ produces 6 and $\texttt{(* 5 2)}$ produces 10, so we must apply the
$\texttt{sum-of-squares}$ procedure to 6 and 10.  These values are substituted for
the formal parameters $\texttt{x}$ and $\texttt{y}$ in the body of
$\texttt{sum-of-squares}$, reducing the expression to

```scheme
(+ (square 6) (square 10))
```


If we use the definition of $\texttt{square}$, this reduces to

```scheme
(+ (* 6 6) (* 10 10))
```


which reduces by multiplication to

```scheme
(+ 36 100)
```


and finally to

```scheme
136
```


The process we have just described is called the _substitution model_
for procedure application.  It can be taken as a model that determines the
“meaning” of procedure application, insofar as the procedures in this chapter
are concerned.  However, there are two points that should be stressed:

- The purpose of the substitution is to help us think about procedure application, not to provide a description of how the interpreter really works. Typical interpreters do not evaluate procedure applications by manipulating the text of a procedure to substitute values for the formal parameters.  In practice, the “substitution” is accomplished by using a local environment for the formal parameters.  We will discuss this more fully in [Chapter 3](3_Modularity_Objects_and_State.md) and [Chapter 4](4_Metalinguistic_Abstraction.md) when we examine the implementation of an interpreter in detail.
- Over the course of this book, we will present a sequence of increasingly elaborate models of how interpreters work, culminating with a complete implementation of an interpreter and compiler in [Chapter 5](5_Computing_with_Register_Machines.md).  The substitution model is only the first of these models---a way to get started thinking formally about the evaluation process.  In general, when modeling phenomena in science and engineering, we begin with simplified, incomplete models.  As we examine things in greater detail, these simple models become inadequate and must be replaced by more refined models.  The substitution model is no exception.  In particular, when we address in [Chapter 3](3_Modularity_Objects_and_State.md) the use of procedures with “mutable data,” we will see that the substitution model breaks down and must be replaced by a more complicated model of procedure application.

### Applicative order versus normal order

According to the description of evaluation given in [Section 1.1.3](1.1.3_Evaluating_Combinations.md), the
interpreter first evaluates the operator and operands and then applies the
resulting procedure to the resulting arguments.  This is not the only way to
perform evaluation.  An alternative evaluation model would not evaluate the
operands until their values were needed.  Instead it would first substitute
operand expressions for parameters until it obtained an expression involving
only primitive operators, and would then perform the evaluation.  If we used
this method, the evaluation of $\texttt{(f 5)}$ would proceed according to the
sequence of expansions

```scheme
(sum-of-squares (+ 5 1) (* 5 2))
(+   (square (+ 5 1))      (square (* 5 2))  )
(+   (* (+ 5 1) (+ 5 1))   (* (* 5 2) (* 5 2)))
```


followed by the reductions

```scheme
(+      (* 6 6)      (* 10 10))
(+         36           100)
                136
```


This gives the same answer as our previous evaluation model, but the process is
different.  In particular, the evaluations of $\texttt{(+ 5 1)}$ and $\texttt{(* 5 2)}$
are each performed twice here, corresponding to the reduction of the expression
$\texttt{(* x x)}$ with $\texttt{x}$ replaced respectively by $\texttt{(+ 5 1)}$ and
$\texttt{(* 5 2)}$.

This alternative “fully expand and then reduce” evaluation method is known as
_normal-order evaluation_, in contrast to the “evaluate the arguments
and then apply” method that the interpreter actually uses, which is called
_applicative-order evaluation_.  It can be shown that, for procedure
applications that can be modeled using substitution (including all the
procedures in the first two chapters of this book) and that yield legitimate
values, normal-order and applicative-order evaluation produce the same value.
(See [Exercise 1.5](List_of_Exercises.md#1-5) for an instance of an “illegitimate” value where
normal-order and applicative-order evaluation do not give the same result.)

Lisp uses applicative-order evaluation, partly because of the additional
efficiency obtained from avoiding multiple evaluations of expressions such as
those illustrated with $\texttt{(+ 5 1)}$ and $\texttt{(* 5 2)}$ above and, more
significantly, because normal-order evaluation becomes much more complicated to
deal with when we leave the realm of procedures that can be modeled by
substitution.  On the other hand, normal-order evaluation can be an extremely
valuable tool, and we will investigate some of its implications in [Chapter 3](3_Modularity_Objects_and_State.md) and [Chapter 4](4_Metalinguistic_Abstraction.md).[^16]

---

[^15]: Despite the simplicity of the substitution idea, it turns out to be surprisingly complicated to give a rigorous mathematical definition of the substitution process.  The problem arises from the possibility of confusion between the names used for the formal parameters of a procedure and the (possibly identical) names used in the expressions to which the procedure may be applied.  Indeed, there is a long history of erroneous definitions of _substitution_ in the literature of logic and programming semantics. See [Stoy 1977](References.md#Stoy-1977) for a careful discussion of substitution.

[^16]: In [Chapter 3](3_Modularity_Objects_and_State.md) we will introduce _stream processing_, which is a way of handling apparently “infinite” data structures by incorporating a limited form of normal-order evaluation.  In [Section 4.2](4.2_Variations_on_a_Scheme_Lazy_Evaluation.md) we will modify the Scheme interpreter to produce a normal-order variant of Scheme.