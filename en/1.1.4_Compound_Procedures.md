### <span class="chapnum">1.1.4</span> Compound Procedures

We have identified in Lisp some of the elements that must appear in any
powerful programming language:

- Numbers and arithmetic operations are primitive data and procedures.
- Nesting of combinations provides a means of combining operations.
- Definitions that associate names with values provide a limited means of
abstraction.


Now we will learn about _procedure definitions_, a much more powerful
abstraction technique by which a compound operation can be given a name and
then referred to as a unit.

We begin by examining how to express the idea of “squaring.”  We might say,
“To square something, multiply it by itself.”  This is expressed in our
language as

```scheme
(define (square x) (* x x))
```


We can understand this in the following way:

```scheme
(define (square    x)         (*      x         x))
  |        |       |           |      |         |
 To     square  something,  multiply  it  by  itself.
```


We have here a _compound procedure_, which has been given the name
$\texttt{square}$.  The procedure represents the operation of multiplying something
by itself.  The thing to be multiplied is given a local name, $\texttt{x}$, which
plays the same role that a pronoun plays in natural language.  Evaluating the
definition creates this compound procedure and associates it with the name
$\texttt{square}$.[^12]

The general form of a procedure definition is

```scheme
(define (⟨<i>name</i>⟩ ⟨<i>formal parameters</i>⟩)
  ⟨<i>body</i>⟩)
```


The ⟨<i>name</i>⟩ is a symbol to be associated with the procedure definition in
the environment.[^13] The ⟨<i>formal parameters</i>⟩ are the names used within the body of the procedure to refer to
the corresponding arguments of the procedure.  The ⟨<i>body</i>⟩ is an
expression that will yield the value of the procedure application when the
formal parameters are replaced by the actual arguments to which the procedure
is applied.[^14]  The ⟨<i>name</i>⟩ and
the ⟨<i>formal parameters</i>⟩ are grouped within parentheses, just as they
would be in an actual call to the procedure being defined.

Having defined $\texttt{square}$, we can now use it:

```scheme
(square 21)
<i>441</i>
(square (+ 2 5))
<i>49</i>
(square (square 3))
<i>81</i>
```


We can also use $\texttt{square}$ as a building block in defining other procedures.
For example, $x^2 + y^2$ can be expressed as

```scheme
(+ (square x) (square y))
```


We can easily define a procedure $\texttt{sum-of-squares}$ that, given any two
numbers as arguments, produces the sum of their squares:

```scheme
(define (sum-of-squares x y)
  (+ (square x) (square y)))
(sum-of-squares 3 4)
<i>25</i>
```


Now we can use $\texttt{sum-of-squares}$ as a building block in constructing
further procedures:

```scheme
(define (f a)
  (sum-of-squares (+ a 1) (* a 2)))
(f 5)
<i>136</i>
```


Compound procedures are used in exactly the same way as primitive procedures.
Indeed, one could not tell by looking at the definition of
$\texttt{sum-of-squares}$ given above whether $\texttt{square}$ was built into the
interpreter, like $\texttt{+}$ and $\texttt{*}$, or defined as a compound procedure.

---

[^12]: Observe that there are two different operations being combined here: we are creating the procedure, and we are giving it the name $\texttt{square}$.  It is possible, indeed important, to be able to separate these two notions---to create procedures without naming them, and to give names to procedures that have already been created.  We will see how to do this in [Section 1.3.2](1.3.2_Constructing_Procedures_Using_lambda.md).

[^13]: Throughout this book, we will describe the general syntax of expressions by using italic symbols delimited by angle brackets---e.g., ⟨<i>name</i>⟩---to denote the “slots” in the expression to be filled in when such an expression is actually used.

[^14]: More generally, the body of the procedure can be a sequence of expressions.  In this case, the interpreter evaluates each expression in the sequence in turn and returns the value of the final expression as the value of the procedure application.